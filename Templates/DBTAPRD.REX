&TRACE OFF
&IF &INDEX = 1 &IF &1 = ? &GOTO -TELL
&GOTO -RUN
-TELL &TYPE FORMAT:     &0 ?1 ?2 ?3 ?4 ...
&BEGTYPE -END

PURPOSE:    TO READ A FILE FROM A TAPE

USAGE:      ?1 - THE VOLUME SERIAL NUMBER OF THE TAPE YOU WANT MOUNTED
            ?2 - THE FILE NAME YOU WANT THE FILE TO HAVE,
            ?3 - THE FILE TYPE YOU WANT THE FILE TO HAVE,
            ?4 - THE FILE MODE OR DISK WHERE THE FILE WILL RESIDE,
            ?5 - THE NO. OF TAPE MARKS TO BE SKIPPED BEFORE READING
            ?6 - RECFM
            ?7 - LRECL
            ?8 - BLKSIZE
            ?9 - CHECK VOLID (Y=YES, N=NO) DEFAULT: N

RETCODES:    0 - EXITED NORMALLY
             9 - USER ABANDONED TASK

AUTHOR:     KGM
CHANGED BY: CRL JAN 86 TO USE DBTAPE2 EXEC
LAST CHANGE: DD MMM YY.
-END
&EXIT 1
-RUN
    CLRSCRN
* SET PARMS
    &VOLID = &1
    &FNAME = &2
    &FTYPE = &3
    &FMODE = &4
    &MARKS = &5
    &RECFM = &6
    &LRECL = &7
    &BLKSIZE = &8
    &IF .&9 ª= . &CHECK = &9
    &IF .&9  = . &CHECK = N
* ATTACH A TAPE DRIVE AND GET THE TAPE READIED
    EXEC DBTAPE2 &VOLID 181 EXIT N &CHECK
    &IF &RC ª= 0 &EXIT 9
* READ PAST THE REQD NO OF TAPE MARKS
    TAPE REW
    &IF &MARKS = 0 &SKIP 3
        &TYPE
        &TYPE &TIME READING TAPE VOLID &VOLID PAST &MARKS TAPE MARKS.
        TAPE FSF &MARKS
* READ IN FILE
    &TYPE
    &TYPE &TIME READING TAPE VOLID &VOLID ONTO DISK AS:
    &TYPE ........ &FNAME &FTYPE &FMODE ( RECFM &RECFM LRECL &LRECL BLKSIZE &BLKSIZE
    FI INFILE CLEAR
    FI INFILE TAP1
    FI OUTFILE CLEAR
    FI OUTFILE DISK &FNAME &FTYPE &FMODE (RECFM &RECFM LRECL &LRECL BLKSIZE &BLKSIZE
    MOVE INFILE OUTFILE
* DISMOUNT TAPE, DETACH DRIVE, SEND MESSAGE TO OPERATOR AND EXIT.
    TAPE RUN
    CP DET 181
    EXECIO 1 CP (LIFO STRING Q V &VADDR
    &READ VARS * * * * &UNIT
    CP M OP TAPE UNIT &UNIT HAS BEEN DETACHED FROM MY MACHINE. KEEP TAPE VOLID &VOLID .
    &TYPE
    &TYPE &TIME TAPE FILE HAS BEEN READ AND TAPE UNIT DETACHED.
    &TYPE
    &EXIT
