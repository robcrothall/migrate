&TRACE OFF
&IF &INDEX = 1 &IF &1 = ? &GOTO -TELL
&GOTO -RUN
-TELL &TYPE FORMAT:     &0 ?1 ?2 ?3 ......
&BEGTYPE -END
PURPOSE:    TO REQUEST THE READYING OF A TAPE ON A UNIT AND CHECKING
            THE VOLID IF REQD.
            IF A UNIT HAS NOT ALREADY BEEN ATTACHED THE EXEC CALLS
            DBTAPE1R EXEC TO ATTACH A UNIT.
            THE TAPE IS REWOUND BEFORE THE NORMAL EXIT.

USAGE:      ?1 - VOLID (6 CHARS MAX)
            ?2 - VIRTUAL TAPE ADDRESS (181,182,183 OR 184) DEFAULT: 181
            ?3 - ACTION IF EXEC INTERRUPTED BY PRESSING ENTER TWICE:
                 ABORT (DEFAULT) - RETURN TO CMS
                 EXIT            - EXIT WITH RETURN CODE OF 9
            ?4 - WRITE RING (Y=YES, N=NO)  DEFAULT: N
            ?5 - CHECK VOLID (Y=YES, N=NO) DEFAULT: N

NOTE:       THE TAPE MUST HAVE A STANDARD VOL1 LABEL TO CHECK VOLID.

AUTHOR:     CRL JAN 86
CHANGED BY: CRL JUL 86 TO USE DBLABCHK EXEC INSTEAD OF LABEL MODULE.
LAST CHANGE: DD MMM YY.
-END
&EXIT 1
-RUN
    DESBUF
* SET PARMS
    &VOLID = &1
    &IF .&2 ª= . &VADDR = &2
    &IF .&2  = . &VADDR = 181
    &IF .&3 ª= . &INT   = &3
    &IF .&3  = . &INT   = ABORT
    &IF .&4 ª= . &RING  = &4
    &IF .&4  = . &RING  = N
    &IF .&5 ª= . &CHECK = &5
    &IF .&5  = . &CHECK = N
* SET RING MESSAGE
    &RINGMESS = WITHOUT
    &IF &RING = Y &RINGMESS = WITH
* CHECK IF UNIT ATTACHED AND ATTACH A UNIT IF NECESSARY
-A1 EXECIO 1 CP (LIFO STRING Q V &VADDR
    &READ VARS * * * * &UNIT
    &IF &RC = 0 &GOTO -A2
    EXEC DBTAPE1R &VADDR &INT
    &IF &RC ª= 0 &EXIT 9
    EXECIO 1 CP (LIFO STRING Q V &VADDR
    &READ VARS * * * * &UNIT
* RUN TAPE IF IT IS CURRENTLY READIED.
-A2 TAPECHK &VADDR
    &IF &RC < 4 TAPE RUN (&VADDR
* MESSAGE TO USER
    &TYPE
    &TYPE &TIME READYING OF TAPE VOLID &VOLID &RINGMESS WRITE RING.
    &TYPE ........ PRESS ENTER TWICE TO GET OUT IF YOU GET TIRED OF WAITING.
    &TYPE
    &IF &CHECK = Y &SKIP 3
        &TYPE NOTE *** THE COMPUTER WILL NOT CHECK THE VOLID.
        &TYPE -------- WE ARE RELYING ON THE OPERATOR TO MOUNT THE CORRECT TAPE.
        &TYPE
* CHECK TAPE AND REPEAT MESSAGES TO OPERATOR EVERY MINUTE IF NECESSARY.
    &SECS = 0
-A3 &SECS = &SECS + 1
    &IF &SECS > 60 &SECS = 1
    TAPECHK &VADDR
   *BRANCH IF TAPE UNIT NOT ATTACHED
    &IF &RC = 8 &GOTO -A1
   *BRANCH IF TAPE MOUNTED WITH WRONG RING SETTING
    &IF &RC = 2 &IF &RING = Y &GOTO -ERR1
    &IF &RC = 0 &IF &RING = N &GOTO -ERR2
   *BRANCH IF TAPE MOUNTED WITH CORRECT RING SETTING
    &IF &RC < 4 &GOTO -A4
   *IF TAPE NOT READIED - SEND READY MESSAGE EVERY MINUTE
    &IF &RC = 4 &IF &SECS > 1 &SKIP 2
    CP M OP READY UNIT &UNIT WITH TAPE VOLID &VOLID &RINGMESS WRITE RING.
    &TYPE &TIME OPERATOR ASKED TO READY TAPE.
    &CALL -WAIT +00:01 &INT OPERATOR
    &GOTO -A3
* REWIND TAPE AND CHECK VOLID IF REQD
-A4 TAPE REW (&VADDR
    &IF &CHECK = N &GOTO -A5
    EXEC DBLABCHK &VOLID &VADDR
   *BRANCH IF VOLID INCORRECT
    &IF &RETCODE ª= 0 &GOTO -ERR3
    TAPE REW (&VADDR
* EXIT NORMALLY - TAPE READIED AND REWOUND
-A5 &TYPE &TIME TAPE READIED AND REWOUND.
    &EXIT
***** ERROR CONDITIONS *************************************************
-ERR1
    CP M OP ADD WRITE RING TO TAPE ON UNIT &UNIT .
    &TYPE &TIME OPERATOR ASKED TO ADD WRITE RING TO TAPE.
    &GOTO -A2
-ERR2
    CP M OP REMOVE WRITE RING FROM TAPE ON UNIT &UNIT .
    &TYPE &TIME OPERATOR ASKED TO REMOVE WRITE RING FROM TAPE.
    &GOTO -A2
-ERR3
    CP M OP WRONG VOLID ON UNIT &UNIT . TAPE MUST HAVE STD LABEL WITH CORRECT VOLID.
    &TYPE &TIME OPERATOR NOTIFIED THAT VOLID CHECK FAILED.
    &GOTO -A2
*************************************************************************
* SUBROUTINE TO WAIT INTERVAL IN PARM 1, AND IF INTERRUPTED BY THE USER *
* PRESSING ENTER TWICE, TO ABORT OR EXIT (RC=9) ACCORDING TO PARM 2.    *
* IF EXEC INTERRUPTED A MESSAGE IS SENT TO PARM 3 TO IGNORE LAST REQUEST*
*************************************************************************
-WAIT
      EXEC NOTYPE WAKEUP &1 (NOEXT CONS
      &IF &RC ª= 6 &RETURN
      CP M &3 IGNORE MY LAST REQUEST - I AM GOING TO DO SOMETHING ELSE.
      &TYPE &TIME TASK ABANDONED - &3 HAS BEEN NOTIFIED.
      &IF .&2 = .EXIT &EXIT 9
      EXEC DBSTOP
      &RETURN
*************************************************************************
