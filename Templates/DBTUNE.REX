&TRACE OFF
*P SPOOL CON START TO ZFAERJC
&IF &INDEX = 1 &IF &1 = ? &GOTO -TELL
&GOTO -RUN
-TELL &TYPE FORMAT:     &0 ?1 ?2 ?3 ?4 ...
&BEGTYPE -END
PURPOSE:    THIS EXEC IS CALLED BY THE APPLIB MACHINE EVERY 3 MINS.
            IT TUNES THE MACHINE TO KEEP RESPONSE AT A REASONABLE LEVEL.

            THIS EXEC EXAMINES THE EXPANSION FACTOR AND IF:

            1. THE EXPANSION FACTOR IS >= &EXP2 (SEE VALUE BELOW)
               **************************************************
               ISSUES A CP IND Q COMMAND AND SEES WHICH MACHINES
               ARE IN Q3. THESE MACHINES HAVE THEIR PRIORITY CHANGED
               TO 99 OR TO THEIR ORIGINAL PRIORITY IF VTAM OR RSCS.
               THE ORIGINAL AND NEW PRIORITIES ARE WRITTEN TO A FILE
               'DBTUNE CURRENT X' WHICH CAN BE VIEWED AT ANY TIME.
               IF THE MACHINE IS ALREADY ON THE LIST ITS PRIORITY
               IS NOT CHANGED AGAIN. HENCE A MACHINE CAN BE MANUALLY
               RESET TO A HIGHER PRIORITY AND IT WILL NOT IMMEDIATELY
               BE RESET TO 99.

               A FILE 'DBTUNE HISTORY X' IS UPDATED WITH THE TOTAL
               TIME THE MACHINE HAS HAD ITS PRIORITY CHANGED AND THE
               DATE AND TIME OF THE LAST CHANGE.

            2. THE EXPANSION FACTOR IS >= &EXP3 (SEE VALUE BELOW)
               **************************************************
               ISSUES A CP IND Q COMMAND AND SEES WHICH MACHINES
               ARE IN Q2 OR Q3 AND RESETS PRIORITIES AS ABOVE.

            3. THE EXPANSION FACTOR IS <= &EXP1 (SEE VALUE BELOW)
               **************************************************
               RESTORES ALL MACHINES ON 'DBTUNE CURRENT X' TO THEIR
               ORIGINAL PRIORITY AND ERASES THE FILE.

N.B. &EXP1/2/3 CAN BE CHANGED AT ANY TIME BY ALTERING THEIR VALUES BELOW
     AND REPLACING THIS EXEC ON THE X-DISK WITH THE APPLIB COMMAND.

USAGE:      NO PARMS

AUTHOR:     CRL AUG 85
CHANGED BY:
LAST CHANGE: DD MMM YY.
-END
&EXIT 1
-RUN
*********** SET THE VALUES OF &EXP1 AND &EXP2 HERE **********************
&EXP1 = 034
&EXP2 = 036
&EXP3 = 038
*************************************************************************
&DTE = &DATE
&TME = &TIME
*GET CURRENT EXPANSION FACTOR
EXECIO 2 CP (LIFO STRING IND
&READ VARS
&READ STRING &A1
&EXP = &SUBSTR OF &A1 63 3
&IF &EXP <= &EXP1 &GOTO -RESTORE
&IF &EXP >= &EXP2 &GOTO -TUNE
*P SPOOL CON STOP
*P SPOOL CON CLOSE
&EXIT
-TUNE
*STACK THE MACHINES IN QUEUES
DESBUF
EXECIO * CP (FIFO STRING IND Q
SENTRIES
&LOOP 4 &RC
      &READ VARS &MAC1 &Q1 * * &MAC2 &Q2 * * &MAC3 &Q3
      &IF .&MAC1 ª= . &IF .&MAC1 ª= .NO &STACK &MAC1 &Q1
      &IF .&MAC2 ª= . &IF .&MAC2 ª= .NO &STACK &MAC2 &Q2
      &IF .&MAC3 ª= . &IF .&MAC3 ª= .NO &STACK &MAC3 &Q3
*PROCESS EACH MACHINE ON QUEUE
SENTRIES
&LOOP -ENDLOOP &RC
      &READ VARS &MAC &Q
     *BYPASS IF Q1
      &IF &Q = Q1 &GOTO -ENDLOOP
     *BYPASS IF Q2 AND EXP FACTOR < &EXP3
      &IF &Q = Q2 &IF &EXP < &EXP3 &GOTO -ENDLOOP
     *SEE IF MACHINE IS ON DBTUNE CURRENT X - IF NOT GO TO ADD
      &MAC = &LEFT OF &MAC 8
      STATE DBTUNE CURRENT X
      &IF &RC ª= 0 &GOTO -ADD
      &ARG = &CONCAT OF / &MAC /
      EXECIO * DISKR DBTUNE CURRENT X 1 (FIND &ARG
      &IF &RC ª= 0 &GOTO -ADD
      &READ VARS
      &READ VARS
      &GOTO -ENDLOOP
     -ADD
     *GET PRIORITY OF MACHINE
      EXECIO 1 CP (LIFO STRING Q PRIOR &MAC
      &READ STRING &DATA
      &IF &RC ª= 0 &GOTO -ENDLOOP
     *CHANGE PRIORITY OF MACHINE TO REQUIRED PRIORITY
      &ORIG = &SUBSTR OF &DATA 21 2
      &TYPE ORIG = &ORIG
      &SET = 099
      &IF &MAC = VTAM     &GOTO -ENDLOOP
      &IF &MAC = RSCSV2   &GOTO -ENDLOOP
      &IF &MAC = SQLDBA   &GOTO -ENDLOOP
      &IF &MAC = CSDSQL   &GOTO -ENDLOOP
      &IF &MAC = DSDSQL   &GOTO -ENDLOOP
      &IF &MAC = IDMS     &GOTO -ENDLOOP
      &IF &MAC = SMART    &GOTO -ENDLOOP
      &IF &MAC = SLIM     &GOTO -ENDLOOP
      &IF &MAC = APPLIB   &GOTO -ENDLOOP
      &IF &MAC = KOFSYBBK &GOTO -ENDLOOP
      &IF &MAC = KOFSYBCM &GOTO -ENDLOOP
      &SET2 = &SUBSTR OF &SET 2 2
      CP SET PRIOR &MAC &SET2
      &TYPE PRIORITY &MAC SET TO &SET
     *WRITE RECORD TO DBTUNE CURRENT X
      &DATA1 = &SUBSTR OF &DATA 1 23
      &DATA2 = &STRING OF SET TO &SET &DTE &TME
      &STACK LIFO &DATA1 &DATA2
      EXECIO 1 DISKW DBTUNE CURRENT X 0 F 80 (FINIS
      &GOTO -ENDLOOP
     -CHANGE
      DESBUF
     -ENDLOOP
*P SPOOL CON STOP
*P SPOOL CON CLOSE
&EXIT
*RESTORE ALL MACHINES ON CURRENT LIST TO THEIR ORIGINAL PRIORITY
-RESTORE
*CHECK IF THERE IS A CURRENT LIST
STATE DBTUNE CURRENT X
&IF &RC ª= 0 &EXIT
DESBUF
FINIS DBTUNE CURRENT X
EXECIO * DISKR DBTUNE CURRENT X (FINIS
SENTRIES
&LOOP -ENDLOP2 &RC
      &READ STRING &DATA
      &MAC = &SUBSTR OF &DATA 1 8
      &ORIG = &SUBSTR OF &DATA 20 3
      &ORIG2 = &SUBSTR OF &ORIG 2 2
      CP SET PRIOR &MAC &ORIG2
      &TYPE PRIORITY &MAC RESTORED TO &ORIG
     *UPDATE HISTORY FILE
      &GOTO -ENDLOP2
     *&ARG = &CONCAT OF / &MAC &BLANK &ORIG /
      &ARG = &CONCAT OF / &MAC /
      EXECIO * DISKR DBTUNE HISTORY A 4 (FIND &ARG
      &IF &RC ª= 0 &GOTO -ENDLOP2
      &READ VARS * &LINE
      &READ STRING &DATA
      &PART1 = &SUBSTR OF &DATA 1 49
      &START = &SUBSTR OF &DATA 26 8
      &TOTDUR = &SUBSTR OF &DATA 69 11
      &SS1 = -CONSECS OF &START
*TYPE START = &START
*TYPE SS1 = &SS1
      &SS2 = -CONSECS OF &TME
*TYPE SS2 = &SS2
      &SS3 = &SS2 - &SS1
*TYPE SS3 = &SS3
      &IF &SS3 < 0 &SS3 = 0
      &DUR = -CONHMS OF &SS3
      &DUR = &CONCAT OF 000 &DUR
      &DUR = &RIGHT OF &DUR 8
      &SS4 = -CONSECS OF &TOTDUR
      &SS4 = &SS4 + &SS3
      &TOTDUR = -CONHMS OF &SS4
      &TOTDUR = &RIGHT OF &TOTDUR 11
      &DATA = &STRING OF &PART1 &TME &DUR &TOTDUR
      &STACK LIFO &DATA
      EXECIO 1 DISKW DBTUNE HISTORY A &LINE F 80 (FINIS
      &IF &RC ª= 0 &GOTO -ENDLOP3
     -ENDLOP2
ERASE DBTUNE CURRENT X
*P SPOOL CON STOP
*P SPOOL CON CLOSE
&EXIT
-ENDLOP3
ERASE DBTUNE CURRENT X
&EXIT 9999
*FUNCTION TO CONVERT H:MM:SS - HHHHH:MM:SS TO SECONDS
    -CONSECS
&TYPE PARM = &1
     &F = &RIGHT OF &1 11
     &HH = &SUBSTR OF &F 1 5
     &MM = &SUBSTR OF &F 7 2
     &SS = &SUBSTR OF &F 10 2
     &MM = &MULT OF &MM 60
     &HH = &TRANS OF &HH &BLANK 0
     &HH = &MULT OF &HH 3600
     &SS = &SS + &MM + &HH
     &RETURN &SS
*FUNCTION TO CONVERT SECONDS TO H:MM:SS - HHHHH:MM:SS
    -CONHMS
     &MM = &DIV OF &1 60
     &XX = &MULT OF &MM 60
     &SS = &1 - &XX
     &HH = &DIV OF &MM 60
     &XX = &MULT OF &HH 60
     &MM = &MM - &XX
     &L = &LENGTH OF &SS
     &IF &L = 1 &SS = &CONCAT OF 0 &SS
     &L = &LENGTH OF &MM
     &IF &L = 1 &MM = &CONCAT OF 0 &MM
     &HMS = &CONCAT OF &HH : &MM : &SS
     &RETURN &HMS
