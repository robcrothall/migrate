&TRACE OFF
&IF &INDEX = 1 &IF &1 = ? &GOTO -TELL
&GOTO -RUN
-TELL &TYPE FORMAT:     &0 ?1 ?2 ?3 ?4 ...
&BEGTYPE -END
PURPOSE:    TO SEND A REQUEST TO APPLIB TO ATTACH A TAPE UNIT TO USERS
            MACHINE. IF NONE AVAILABLE THE EXEC WILL CONTINUE TO ASK THE
            OPERATOR TO MAKE A UNIT AVAILABLE EVERY 2-3 MINUTES.
            THE EXEC TYPES A RUNNING COMMENTARY.

USAGE:      ?1 - VIRTUAL TAPE ADDRESS (181,182,183 OR 184) DEFAULT: 181
            ?2 - ACTION IF EXEC INTERRUPTED BY PRESSING ENTER TWICE:
                 ABORT (DEFAULT) - RETURN TO CMS
                 EXIT            - EXIT WITH RETURN CODE OF 9

NOTE:       THIS EXEC USES APPLIB TO DO THE TAPE ATTACHING AND
            COMMUNICATES WITH APPLIB VIA SMSG'S.

            A LIST OF TAPE UNIT ABSOLUTE ADDRESSES WHICH ARE AVAILABLE
            TO THIS EXEC FOR ATTACHING ARE INCLUDED IN THIS EXEC AND
            SHOULD BE UPDATED HERE IF THEY CHANGE.

AUTHOR:     CRL JAN 86
CHANGED BY:
LAST CHANGE: DD MMM YY.
-END
&EXIT 1
-RUN
****** MAINTAIN THE LIST OF ABSOLUTE TAPE UNIT ADRESSES HERE (MAX 10) ***
*                    --- --- --- --- --- --- --- --- --- ---
 &UNITS = &STRING OF 399 398 397 396 395 394 393 392 391 390
*                    --- --- --- --- --- --- --- --- --- ---
*************************************************************************
      DESBUF
* SET PARMS
      &IF .&1 ª= . &VADDR = &1
      &IF .&1  = . &VADDR = 181
      &IF .&2 ª= . &INT   = &2
      &IF .&2  = . &INT   = ABORT
* CHECK IF UNIT ALREADY ATTACHED
      EXECIO 0 CP (LIFO STRING Q V &VADDR
      &IF &RC = 0 &EXIT
* MESSAGE TO USER
      &TYPE
      &TYPE &TIME ATTACHING A TAPE DRIVE AS YOUR VIRT &VADDR TAPE UNIT.
      &TYPE ........ PRESS ENTER TWICE TO GET OUT IF YOU GET TIRED OF WAITING.
      &TYPE
* CHECK IF APPLIB IS LOGGED ON OR DISCONNECTED
-A1
      EXECIO 0 CP (STRING M APPLIB PLEASE PUT IN DISCONNECTED MODE ASAP.
      &IF &RC = 0    &GOTO -A2
      &IF &RC = 1057 &GOTO -A3
* APPLIB IS NOT LOGGED ON - NOTIFY OPERATOR
      &TYPE &TIME WAITING 3 MINS FOR APPLIB TO BE LOGGED ON BY OPERATOR.
      M OP PLEASE AUTOLOG APPLIB MACHINE.
      &CALL -WAIT +03:00 &INT OPERATOR
      &GOTO -A1
* APPLIB IS LOGGED ON BUT NOT DISCONNECTED
-A2
      &TYPE &TIME WAITING 3 MINS FOR APPLIB TO BE DISCONNECTED.
      &CALL -WAIT +03:00 &INT APPLIB
      &GOTO -A1
* APPLIB IS DISCONNECTED - ATTACH TAPE UNIT VIA APPLIB SMSG
* SEND OPERATOR MESSAGE EVERY 3+ MINS TO RELEASE A UNIT IF NONE
-A3
      CP SET SMSG ON
      &SECS = 0
-A4
      &SECS = &SECS + 30
      &IF &SECS > 180 &SECS = 30
     *SEND SMSG TO APPLIB
      CP SMSG APPLIB DBTAPEAT &VADDR &UNITS
      &IF &RC ª= 0 &GOTO -A1
      &CALL -WAIT +00:30 &INT OPERATOR
     *CHECK IF UNIT ATTACHED AND GET UNIT ADDRESS
      EXECIO 1 CP (LIFO STRING Q V &VADDR
      &READ VARS * * * * &UNIT
      &IF &RC = 0 &GOTO -A5
     *ALLOW 30 SECS FOR APPLIB TO ATTACH TAPE BEFORE TELLING OPERATOR
      &IF &SECS ª= 30 &GOTO -A4
      CP M TAPOPER TRYING TO ATTACH A TAPE UNIT. PSE FREE A UNIT A.S.A.P. (IN THE RANGE 390-399)
      CP M XPER TRYING TO ATTACH A TAPE UNIT. PSE FREE A UNIT A.S.A.P. (IN THE RANGE 390-399)
      &RX = &RC
      &IF &RX ª= 45 &GOTO -XX1
      CP M OP *****   TAPOPER ---  NOT LOGGED ON ----   PLEASE CORRECT
      CP M OP TRYING TO ATTACH A TAPE UNIT. PSE FREE A UNIT A.S.A.P. (IN THE RANGE 390-399)
      &GOTO -XX2
-XX1
      &IF &RX ª= 57 &GOTO -XX2
      CP M OP *****   TAPOPER --- DISCONNECTED ----   PLEASE CORRECT
      CP M OP TRYING TO ATTACH A TAPE UNIT. PSE FREE A UNIT A.S.A.P. (IN THE RANGE 390-399)
-XX2
      &TYPE &TIME NO TAPE UNITS AVAILABLE - HAVE ASKED OPERATOR TO FREE A UNIT.
      &GOTO -A4
* A UNIT HAS BEEN ATTACHED, INFORM THE OPERATOR
-A5
      CP M TAPOPER I HAVE ATTACHED TAPE UNIT &UNIT TO MYSELF AND WILL DETACH IT WHEN FINISHED.
      &RX = &RC
      &IF &RX ª= 45 &GOTO -XX3
      CP M OP *****   TAPOPER ---  NOT LOGGED ON ----   PLEASE CORRECT
      CP M OP I HAVE ATTACHED TAPE UNIT &UNIT TO MYSELF AND WILL DETACH IT WHEN FINISHED.
      &GOTO -XX4
-XX3
      &IF &RX ª= 57 &GOTO -XX4
      CP M OP *****   TAPOPER --- DISCONNECTED ----   PLEASE CORRECT
      CP M OP I HAVE ATTACHED TAPE UNIT &UNIT TO MYSELF AND WILL DETACH IT WHEN FINISHED.
-XX4
      &TYPE &TIME UNIT &UNIT NOW ATTACHED AS YOUR VIRT &VADDR TAPE UNIT.
      &TYPE ........ PLEASE ENSURE THAT IT IS DETACHED AFTER USE.
      &EXIT
*************************************************************************
* SUBROUTINE TO WAIT INTERVAL IN PARM 1, AND IF INTERRUPTED BY THE USER *
* PRESSING ENTER TWICE, TO ABORT OR EXIT (RC=9) ACCORDING TO PARM 2.    *
* IF EXEC INTERRUPTED A MESSAGE IS SENT TO PARM 3 TO IGNORE LAST REQUEST*
*************************************************************************
-WAIT
      EXEC NOTYPE WAKEUP &1 (NOEXT CONS
      &IF &RC ª= 6 &RETURN
      CP M &3 IGNORE MY LAST REQUEST - I AM GOING TO DO SOMETHING ELSE.
      &RX = &RC
      &IF &3 ª= TAPOPER &GOTO -XX6
      &IF &RX ª= 45 &GOTO -XX5
      CP M OP *****   TAPOPER ---  NOT LOGGED ON ----   PLEASE CORRECT
      CP M OP IGNORE MY LAST REQUEST - I AM GOING TO DO SOMETHING ELSE.
      &GOTO -XX6
-XX5
      &IF &RX ª= 57 &GOTO -XX6
      CP M OP *****   TAPOPER --- DISCONNECTED ----   PLEASE CORRECT
      CP M OP IGNORE MY LAST REQUEST - I AM GOING TO DO SOMETHING ELSE.
-XX6
      &TYPE &TIME TASK ABANDONED - &3 HAS BEEN NOTIFIED.
      &IF .&2 = .EXIT &EXIT 9
      EXEC DBSTOP
      &RETURN
*************************************************************************
